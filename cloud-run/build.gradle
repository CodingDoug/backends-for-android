buildscript {
    ext {
        kotlin_version = '1.4.31'
        kotlinx_serialization_version = '1.1.0'
        ktor_version = '1.5.2'
    }

    repositories {
        google()
        mavenCentral()
        // Still needed for shadow plugin
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-gson:$ktor_version"

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.google.cloud:google-cloud-logging-logback:0.120.2-alpha'

    implementation 'com.google.firebase:firebase-admin:7.1.0'
}

application {
    mainClassName = 'com.hyperaware.bfa.cloudrun.MainKt'
}

archivesBaseName = 'app'
jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

shadowJar {
    // This is required to prevent problems with gRPC service metadata
    mergeServiceFiles()
}